ext {
    generatedDir = 'src/generated/java'
    xsd = "$rootDir/xsd/credit.xsd"
}

sourceSets {
    main.java {
        srcDirs += generatedDir
    }
}

configurations {
    jaxb
}

dependencies {
    jaxb "com.sun.xml.bind:jaxb-core:$jaxbVersion"
    jaxb "com.sun.xml.bind:jaxb-impl:$jaxbVersion"
    jaxb "com.sun.xml.bind:jaxb-xjc:$jaxbVersion"
}

task xjc {
    inputs.file(xsd)
    outputs.dir(generatedDir)

    doLast {
        mkdir(generatedDir)

        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.jaxb.asPath)
        ant.xjc(schema: xsd, destdir: generatedDir, package: 'credit.server')
    }
}

compileJava {
    dependsOn(xjc)
}

clean {
    delete(generatedDir)
}
